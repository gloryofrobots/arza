include arza:lang:_interfaces (Instance, Singleton, Any)
import arza:lang:_std_behavior as std_behavior

import arza:lang:_datatype

import arza:lang:_process

include arza:lang:_types (Bool, Char, Int, Float, Symbol, String, List, Vector, Tuple, Map,
                              Function, Partial, Generic, FiberChannel, Coroutine, Interface, Datatype,
                              Module, Array, AssocArray, PID)


include arza:lang:_bit (bitand, bitor, bitxor, bitnot, lshift, rshift)
include arza:lang:_number (abs)


"""
super BUG
io:print(
    #b,
    match 2
        | x = #c
  )
"""


"""
SOME OF THE OPERATORS DEFINED INTERNALLY AND SOME BELOW
Precedence    Operator
    100           : .
    95           x() .{ .[ ->
    60           :: :::
    55           **
    50           *  /
    40           +  - ++
    35           ==  !=  <  <=  >  >=
    30           and
    25           or << >>
    20           |>
    15           " ." as of <| <-
    10           = :=
"""

infixr (:=, :=, 10)
// infix @ as of -> 15
infixr (<-, <-, 15)
infixr (!, __send__, 15)
infixl (<|, <|, 15)
infixl (|>, |>, 20)
// infix or -> 25
infixl (<<, <<, 25)
infixl (>>, >>, 25)
// infix and -> 30
infixl (<, <, 35)
infixl (>, >, 35)
infixl (>=, >=, 35)
infixl (<=, <=, 35)
infixl (==, ==, 35)
infixl (!=, !=, 35)
infixl (++, ++, 40)
infixl (+, +, 40)
infixl (-, -, 40)
infixl (*, *, 50)
infixl (/, /, 50)
// use qualified name to prevent infinite loops in cases of declaring local negate function using prefix -
prefix (-, arza:lang:negate, 55)
// infix :: -> 60
infixl (**, **, 60)
// prefix # -> 70
prefix (&, &, 70)
prefix (&&, &&, 70)
infixl (.., .., 90)
// infix (  .{ .[ -> 95
prefix (~, ~, 96)
// infix . : -> 100


////////////////////////////////////////////////////////////
///////////////// INTERFACES ///////////////////////////////
////////////////////////////////////////////////////////////

interface Eq(T1, T2) =
    ==(T1, T2)
    !=(T1, T2)

interface Comparable(T1, T2) =
    cmp(T1, T2)

interface LessEq(T1, T2) =
    use(Eq)
    <=(first of I, second)

interface Ord(T1, T2) =
    use(<=, cmp)
    <(T1, T2)
    >=(T1, T2)
    >(T1, T2)
    max(T1, T2)
    min(T1, T2)

interface Num(I) =
    -(x of I, y)
    +(x of I, y)
    *(x of I, y)
    /(x of I, y)
    mod(x of I, y)
    // unary -
    negate(x of I)

interface Pow(I) =
    **(x of I, y)

interface Str =
    str(Str)

interface Repr(I) is Str =
    repr(I)

interface Len =
    len(Len)

interface IsEmpty(I) =
    is_empty(I)

interface Put(I) =
    put(key, value, I)

interface Del(I) =
    del(obj, I)

interface At(I) =
    at(key, I)
    elem(key, I)

interface Coll is (Put, At, Del)

interface Dict(I) is Coll =
    keys(self of I)
    values(I)

interface Seq =
    first(Seq)
    rest(Seq)

interface Empty(I) =
    empty(I)

interface Cons(I) =
    cons(value, I)

interface Prepend(I) =
    prepend(value, I)

interface Append(I) =
    append(value, I)

interface Concat(I) =
    ++(I, other)

interface Indexed(I) =
    index_of(obj, I)

interface ToSeq(I) =
    to_seq(I)

interface FromSeq(I) =
    from_seq(I)

interface Slice(I) =
    slice (first, last, I)
    drop(count, I)
    take(count, I)

interface Bound(I) =
    lower_bound(I)
    upper_bound(I)

interface Range(I) =
    range(I, last)
    range_by (I, last, step)
    range_from(I)
    range_from_by(I, step)

interface Ref(I) =
    &(I)

interface MutRef(I) =
    use &(I)
    :=(I, value)

interface Replace(I) =
    <- (I, value)

interface Extract(I) =
    &&(I)

interface Call(I) =
    call(I, args)

interface Cast(I) =
    cast(valueof to_what, I)

// Bootstrap interfaces behavior


trait (T) for Instance =
    def index_of (val, self of T) as _datatype:record_index_of
    def keys(self of T) as _datatype:record_keys
    def values(self of T) as _datatype:record_values

    def put (k, v, self of T) as std_behavior:put
    def at (k, self of T) as std_behavior:at
    def del (el, self of T) as std_behavior:del
    def elem (el, self of T) as std_behavior:elem
    def len (self of T) as std_behavior:len
    def is_empty (self of T) as std_behavior:is_empty
    def cast(to_what, self of T) as std_behavior:cast


trait (T)
    for [Singleton, Function, Generic, Partial,
        Datatype, Bool, Instance, Interface, PID, Module] =

    def == (self of T, other) as std_behavior:equal
    def != (self of T, other) as  std_behavior:not_equal

    def str (self of T) as std_behavior:str
    def repr (self of T) as std_behavior:repr


def at (k, self of Map) as std_behavior:at
def at (el, self of Tuple) as std_behavior:at


////////////////////////////////////////////////////////////
///////////////// TYPES ////////////////////////////////////
////////////////////////////////////////////////////////////

type LT
type GT
type EQ
type Some (val)
type None

// Various Errors
type Error(args)
type ImportError(...Error)
type RuntimeError(...Error)
type TypeError(...Error)
type NotImplementedError(...Error)
type ZeroDivisionError(...Error)
type MathDomainError(...Error)
type ReferenceError(...Error)
type ConstructorError(...Error)
type KeyError(...Error)
type ValueError(...Error)
type SliceError(...Error)
type IndexError(...Error)
type InvokeError(...Error)
type InvalidArgCount(...Error)
type MethodInvokeError(...Error)
type MethodNotImplementedError(...Error)
type MethodSpecializeError(...Error)
type CompileError(...Error)
type ParseError(...Error)
type UnpackSequenceError(...Error)
type FiberFlowError(...Error)
type MatchError(...Error)
type FunctionArgumentsMatchError(...Error)
type ExceptionMatchError(...Error)
type ExportError(...Error)
type ImplementationError(...Error)
type ConstraintError(...Error)
type EmptySeqError(...Error)
type OverflowError(...Error)
//Signal(...d) to interrupt recursive processes
type Interrupt(...Error)
type MessageMatchError(...Error)
type RangeError(...Error)
type CoroutineEmpty(...Error)

////////////////////////////////////////////////////////////
///////////////// FUNCTIONS ////////////////////////////////
////////////////////////////////////////////////////////////

fun put_default(key, default, coll) =
    if elem(key, coll) then
        coll
    else
        put(key, default, coll)

type Lense(source, getter, setter)

fun lense(source, getter, setter) =
    let
        branch = getter(source)
    in
        if not kindof(branch, Coll) then
            throw TypeError("Branch is not a Coll")
        else
            Lense(source, getter, setter)

fun lookup (key, default, coll) =
    try
        at(key, coll)
    catch
        | e of KeyError = default
        | e = throw e

fun not_elem(x, c) = not elem(x, c)

fun identity(x) = x

fun ~ (func) = curry(func)
fun .. (f, g) = curry(f)(g)

fun |>(x, f) = f(x)
fun <|(f, x) = f(x)
fun >>(f, g) = x -> g(f(x))
fun <<(f, g) = x -> f(g(x))

fun twice(f) = f >> f
fun flip (f, x, y) = f(y, x)
fun even(x) = mod(x, 2) == 0
fun odd(x) = not even(x)
fun inc(x) = x + 1
fun dec(x) = x - 1

fun last(coll) = at(len(coll) - 1, coll)

fun downcast(any) = any as Instance

let
    fst = c -> at(0, c)
    snd = c -> at(1, c)
    thrd = c -> at(2, c)
    frth = c -> at(3, c)

let
    self = _process:self
    spawn = _process:spawn


fun extends((fields, _init), parent) =
    let
        new_init =
            match _init
            | () = _datatype:get_init(parent)
            | fn = fn
    in
        (_datatype:get_fields(parent) ++ fields, new_init)


fun setfield((fields, _init), field, value) =
    let
        fun _wrap(...args) =
            let
                data = _init(...args)
            in
                data.{(field) = value}
    in
        (fields, _wrap)


fun super(_type, obj, ...args) =
    if _datatype:has_init(_type) then
        let _init = _datatype:get_init(_type)
        in _init(obj, ...args)
    else
        let
            l = len(args)
            fun _loop(obj, index, args) =
                if index >= l then
                    obj
                else
                    _loop(obj.{(index) = args.[index]}, index + 1, args)
        in
            _loop(obj, 0, args)


// special __at__ method which can be used for default access in case of overloading at
let __at__ = std_behavior:at

fun __send__ (p of PID, msg) = _process:send(p, msg)

def <- (p of PID, msg) as __send__

fun __override__(generic, types, func) = func(method(generic, types))


fun __receive__(fn) =
    let
        proc = self()
//        _ = PL("__RECEIVE__", proc)
        fun _loop() =
            if not _process:is_empty(proc) == False then
                _process:pause(proc)
                _loop()
            else
                let
                    msg = _process:pop(proc)
                in
//                    PL("@@POP", proc, msg, _process:mailbox(proc))
                    fn(msg)
    in
        _loop()
