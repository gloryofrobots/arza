import arza:lang:_std_behavior as std_behavior

from arza:lang:_datatype import (record_values, record_keys, record_index_of)


from arza:lang:_types import (Bool, Char, Int, Float, Symbol, String, List, Vector, Tuple, Map,
                              Function, Partial, Generic, FiberChannel, Coroutine, Interface, Datatype,
                              Env, Array, AssocArray, PID)

from arza:lang:_interfaces import (Instance, Singleton, Any)

from arza:lang:_bit import (bitand, bitor, bitxor, bitnot, lshift, rshift)
from arza:lang:_number import (abs)
import arza:lang:_process

"""
SOME OF THE OPERATORS DEFINED INTERNALLY AND SOME BELOW
Precedence    Operator
    100           : .
    95           x() .{ .[ ->
    60           :: :::
    55           **
    50           *  /
    40           +  - ++
    35           ==  !=  <  <=  >  >=
    30           and
    25           or << >>
    20           |>
    15           " ." @ as of <| <-
    10           = :=
"""

infixr (:=, :=, 10)
// infix @ as of -> 15
infixr (<-, <-, 15)
infixr (!, __send__, 15)
infixl (<|, <|, 15)
infixl (|>, |>, 20)
// infix or -> 25
infixl (<<, <<, 25)
infixl (>>, >>, 25)
// infix and -> 30
infixl (<, <, 35)
infixl (>, >, 35)
infixl (>=, >=, 35)
infixl (<=, <=, 35)
infixl (==, ==, 35)
infixl (!=, !=, 35)
infixl (++, ++, 40)
infixl (+, +, 40)
infixl (-, -, 40)
infixl (*, *, 50)
infixl (/, /, 50)
// use qualified name to prevent infinite loops in cases of declaring local negate function using prefix -
prefix (-, arza:lang:negate, 55)
// infix :: -> 60
infixl (**, **, 60)
// prefix # -> 70
prefix (!, !, 70)
prefix (!!, !!, 70)
infixl (.., .., 90)
// infix (  .{ .[ -> 95
prefix (&, &, 96)
// infix . : -> 100

////////////////////////////////////////////////////////////
///////////////// INTERFACES ///////////////////////////////
////////////////////////////////////////////////////////////

interface PartialEq =
    fun ==(@x, y)

interface Eq is PartialEq =
    fun !=(@x, y)

interface Comparable =
    fun cmp(@first, second)

interface LessEq is PartialEq =
    fun <=(@first, second)

interface Ord =
    use <=(@first, second)
    use cmp(@first, second)
    fun <(@first, second)
    fun >=(@first, second)
    fun >(@first, second)
    fun max(@first, second)
    fun min(@first, second)

interface Num =
    fun -(@x, y)
    fun +(@x, y)
    fun *(@x, y)
    fun /(@x, y)
    fun mod(@x, y)
    // unary -
    fun negate(x)

interface Pow =
    fun **(@x, y)

interface Str =
    fun str(self)

interface Displayed is Str =
    fun repr(self)

interface Len =
    fun len(self)

interface Sized is Len =
    fun is_empty(self)

interface Coll =
    fun put(key, value, @self)
    fun del(obj, @self)

interface GrowableCollection =
    use put(key, value, @self)

interface ShrinkableCollection =
    use del(obj, @self)

interface ReadOnlyCollection =
    fun at(key, @self)
    fun elem(key, @self)

interface Collection is (GrowableCollection, ShrinkableCollection, ReadOnlyCollection)

interface Dict is Collection =
    fun keys(self)
    fun values(self)

interface Seq =
    fun first(self)
    fun rest(self)

interface Emptiable =
    fun empty(self)

interface Consable =
    fun cons(value, @self)

interface Prependable =
    fun prepend(value, @self)

interface Appendable =
    fun append(value, @self)

interface Concatable =
    fun ++(self, @other)

interface Indexed =
    fun index_of(obj, @self)

interface Seqable =
    fun to_seq(self)

interface Castable =
    fun cast(to_what, @self)

interface Sliceable =
    fun slice (first, last, @self)
    fun drop(count, @self)
    fun take(count, @self)

interface Bounded =
    fun lower_bound(self)
    fun upper_bound(self)

interface Ranged =
    fun range(@self, last)
    fun range_by (@self, last, step)
    fun range_from(@self)
    fun range_from_by(@self, step)

interface Ref =
    fun !(self)

interface MutRef =
    use !(self)
    fun :=(@self, value)

interface Channel =
    fun <- (@self, value)

interface Extract =
    fun !!(self)

interface Callable =
    fun call(@callable, args)

////////////////////////////////////////////////////////////
///////////////// TYPES ////////////////////////////////////
////////////////////////////////////////////////////////////

type LT
type GT
type EQ
type Some (val)
type None

// Various Errors
type ImportError(args)
type RuntimeError(args)
type TypeError(args)
type NotImplementedError(args)
type ZeroDivisionError(args)
type MathDomainError(args)
type ReferenceError(args)
type ConstructorError(args)
type KeyError(args)
type ValueError(args)
type SliceError(args)
type IndexError(args)
type InvokeError(args)
type InvalidArgCount(args)
type MethodInvokeError(args)
type MethodNotImplementedError(args)
type MethodSpecializeError(args)
type CompileError(args)
type ParseError(args)
type UnpackSequenceError(args)
type FiberFlowError(args)
type MatchError(args)
type FunctionArgumentsMatchError(args)
type ExceptionMatchError(args)
type ExportError(args)
type ImplementationError(args)
type ConstraintError(args)
type EmptySeqError(args)
type OverflowError(args)
//Signal used to interrupt recursive processes
type Interrupt(result)
type MessageMatchError(args)
type RangeError(args)

// Bootstrap interfaces behavior

trait TInstance(T) for Instance =
    def index_of (val, self of T) as record_index_of
    def keys(self of T) as record_keys
    def values(self of T) as record_values

    def put (k, v, self of T) as std_behavior:put
    def at (k, self of T) as std_behavior:at
    def del (el, self of T) as std_behavior:del
    def elem (el, self of T) as std_behavior:elem
    def len (self of T) as std_behavior:len
    def is_empty (self of T) as std_behavior:is_empty
    def cast(to_what, self of T) as std_behavior:cast

trait TSingleton(T)
    for [Singleton, Function, Generic, Partial,
        Datatype, Bool, Instance, Interface] =

    def == (self of T, other) as std_behavior:equal
    def != (self of T, other) as  std_behavior:not_equal

    def str (self of T) as std_behavior:str
    def repr (self of T) as std_behavior:repr

def at (k, self of Map) as std_behavior:at
def at (el, self of Tuple) as std_behavior:at


////////////////////////////////////////////////////////////
///////////////// FUNCTIONS ////////////////////////////////
////////////////////////////////////////////////////////////

type Lense(source, getter, setter)

fun lense(source, getter, setter) =
    let
        branch = getter(source)
    in
        if not kindof(branch, Collection) then
            throw TypeError("Branch is not a Collection")
        else
            Lense(source, getter, setter)

fun lookup (key, default, coll) =
    try
        at(key, coll)
    catch
        | e of KeyError = default
        | e = throw e

fun not_elem(x, c) = not(elem (x, c))

fun identity(x) = x

fun & (func) = arza:lang:defpartial(func)
fun .. (f, g) = arza:lang:defpartial(f)(g)

fun |>(x, f) = f(x)
fun <|(f, x) = f(x)
fun >>(f, g) = x -> g(f(x))
fun <<(f, g) = x -> f(g(x))

fun twice(f) = f >> f
fun flip (f, x, y) = f(y, x)
fun even(x) = mod(x, 2) == 0
fun odd(x) = not(even(x))
fun inc(x) = x + 1
fun dec(x) = x - 1
//fun last coll = at ((len coll) - 1) coll
fun last(coll) = at(len(coll) - 1, coll)

fun downcast(any of Instance) = any as Instance

let
    fst = c -> at(0, c)
    snd = c -> at(1, c)
    thrd = c -> at(2, c)
    frth = c -> at(3, c)

let
    self = _process:self
    spawn = _process:spawn

fun __send__ (p of PID, msg) = _process:send(p, msg)

def <- (p of PID, msg) as __send__

fun __override__(generic, types, func) = func(method(generic, types))

fun __receive__(fn) =
    let
        proc = self()
        fun _loop() =
            if not _process:is_empty(proc) == False then
                _process:pause(proc)
                _loop()
            else
                let
                    msg = _process:pop(proc)
                in
    //                PL("@@POP", msg, process:mailbox(proc))
                    fn(msg)
    in
        _loop()
