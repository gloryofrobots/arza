import io
import affirm
import seq

fun test_default_values() =
    let
        v = {x=1, y=2}
        v1 = v.{x or 42, z or 42, y = 42}
        l = [0, 1, 2, 3]
        l1 = l.{0 or 5}
        d = {x=1, m={t={y=2, z=0}}}
        dl = $(d.m.t)
        d1 = dl.{y = 100, a or 200}
    in
        affirm:is_equal(v1, {y = 42, x = 1, z = 42})
        affirm:is_equal(l1, l)
        affirm:is_equal(&&d1, {z=0, y=100, a=200})
        affirm:is_equal(&d1, {x=1, m={t={z=0, y=100, a=200}}})

fun test_map() =
    let
        d = {
            y = 2,
            s1 = {
                (True) = False,
                s2 = {
                    x = 1,
                    s3 = {
                        a = 10
                    }
                }
            }
        }
        d1 = d.{
             s1.True = not @,
             s1.s2.x = @ + 1,
             s1.s2 = @.{
                x=42,
                z=24
             },
             s1.s2 = @.{
                s3 = @.{
                    a = @ - 30,
                    b = 20
                }
             },
             s1.s2.x = @ - 66,
             y = (@ +
                  @/2.0*@ *
                  seq:reduce(`+`, [@, @, @])
                  ) + `*`(@, @)
        }
    in
        affirm:is_equal(d1, {y=18.0, s1={s2={z=24, x=-24, s3={b=20, a=-20}}, (True)=True}})

fun test_list() =
    let
        d =[
            [0,1,2],
            3,
            4,
            [5,6,7, [8, 9, [10]]]]
        d1 = d.{
            0 = seq:map((x) -> x * x, @),
            1 = @ * @,
            2 = @,
            (3).(3) = @.{
                0 = @ * 8,
                1 = @ * 9
            },
            (3).(3).((fun () = 2)()).0 = ((x) -> @ * x)(4.2)
        }
    in
        affirm:is_equal(d1, [[0, 1, 4], 9, 4, [5, 6, 7, [64, 81, [42.0]]]])

fun test_lenses() =
    let
        d = {
            s1 = {
                s2 = {
                    x = 1,
                    y = 2
                }
            }
        }
        l1 = $(d.s1.s2)
        l2 = l1.{x = 0, z = 4}
        l3 = l2.{a=42}
        l4 = l1 <- {a=1, b=2, c=3}
        l5 = l1({w=15, t=16})
    in
        affirm:is_equal([&l4, l4.a, l4.b, l4.c], [{s1={s2={b=2, c=3, a=1}}}, 1, 2, 3 ])
        affirm:is_equal([&l3, l3.a], [{s1={s2={z=4, x=0, y=2, a=42}}}, 42])
        affirm:is_equal(&l2, {s1={s2={z=4, x=0, y=2}}})
        affirm:is_equal(&l1, {s1={s2={x=1, y=2}}})
        affirm:is_equal(&&l3, {z=4, x=0, y=2, a=42})
        affirm:is_equal(&&l5, {w=15, t=16})
        affirm:is_equal(l5(), {w=15, t=16})
        // Lenses can be created only for subbranches not for atomic properties
        affirm:is_throw_kindof(() -> $(d.s1.s2.x), (), TypeError)


fun test() =
    test_map()
    test_list()
    test_lenses()
    test_default_values()


