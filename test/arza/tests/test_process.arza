import affirm
import process


fun test_simple() =
    let
        fun ping(n, pong_pid, store) =
            if n == 0 then
                pong_pid ! #finished
                store
            else
                pong_pid ! (#ping, self())
                receive #pong =
                        ping(n-1, pong_pid, #pong :: store)

        fun pong(store) =
            receive
                | #finished =
                    store
                    throw ValueError(store)

                | (#ping, ping_pid) =
                    ping_pid!#pong
                    pong(#ping :: store)

        pong_pid = spawn .. pong .. ([],)
        _ = process:set_error_print_enabled(pong_pid, False)
        ping_pid = spawn .. ping .. (3, pong_pid, [])
        result = process:wait_for([pong_pid, ping_pid])
    in
        affirm:is_equal(result.[ping_pid], [#pong, #pong, #pong])
        affirm:is_equal(result.[pong_pid], ValueError([#ping, #ping, #ping]))

fun test_coro() =
    let
        fun test1() =
            let
                c = process:coro((fun(yield, start) =
                    (let
                        x = yield(start)
                    in
                        yield(x)
                    )
                ))
                c1 = process:coro((yield) -> #zero)
            in
                affirm:is_equal .. c1() .. #zero
                affirm:is_throw(c1, ())

                affirm:is_equal .. c(#first) .. #first
                affirm:is_equal .. c(#second) .. #second
                affirm:is_equal .. c(#last) .. #last
                affirm:is_throw(c, ())
                affirm:is_throw(c, ())
    in
        test1()


fun test() =
    test_coro()
    test_simple()
